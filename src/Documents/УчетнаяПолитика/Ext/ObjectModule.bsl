
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.УчетнаяПолитика.Записывать = Истина;
	Движение = Движения.УчетнаяПолитика.Добавить();
	Движение.Период = Дата;
	Движение.СпособУчетаЗапасов = СпособСписанияЗапасов;
	
	Если СпособСписанияЗапасов = Перечисления.СпособыСписанияЗапасов.ПоСредней Тогда
		
		Движения.ТоварыНаСкладах.Записывать = Истина;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыНаСкладахОстатки.Склад КАК Склад,
		|	ТоварыНаСкладахОстатки.СрокГодности КАК СрокГодности,
		|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Количество,
		|	ТоварыНаСкладахОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(&ДатаОтбора, СрокГодности <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ТоварыНаСкладахОстатки
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Сумма)
		|ПО
		|	Склад,
		|	Номенклатура";
		Запрос.УстановитьПараметр("ДатаОтбора", Дата);
		
		ВыборкаПоСкладу = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоСкладу.Следующий() Цикл
			
			ВыборкаПоНоменклатуре = ВыборкаПоСкладу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаПоНоменклатуре.Следующий() Цикл
				
				Движение = Движения.ТоварыНаСкладах.ДобавитьПриход();
				Движение.Период = Дата;
				ЗаполнитьЗначенияСвойств(Движение, ВыборкаПоНоменклатуре);
				
				ВыборкаДетальные = ВыборкаПоНоменклатуре.Выбрать();
				
				Пока ВыборкаДетальные.Следующий() Цикл
					
					Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
					Движение.Период = Дата;
					ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальные); 
					
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры
