Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.Начисления.Записывать = Истина;
	Движения.УчетЗатрат.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
	Для Каждого ТекСтрокаНачисления Из Начисления Цикл
		
		Движение = Движения.Начисления.Добавить();
		
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаНачисления.ВидРасчета;
		Движение.ПериодДействияНачало = НачалоМесяца(ПериодНачисления);
		Движение.ПериодДействияКонец = КонецМесяца(ПериодНачисления);
		Движение.ПериодРегистрации = ПериодНачисления;
		Движение.БазовыйПериодНачало = НачалоМесяца(ПериодНачисления);
		Движение.БазовыйПериодКонец = КонецМесяца(ПериодНачисления);
		Движение.Сотрудник = ТекСтрокаНачисления.Сотрудник;
		Движение.ПоказательРасчета = ТекСтрокаНачисления.ПоказательРасчета;
		
	КонецЦикла;
	
	Движения.Начисления.Записать();
	Движения.Начисления.РассчитатьСуммуНачисления(); 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Сумма КАК Сумма,
	|	Начисления.ВидРасчета.СтатьяЗатрат КАК СтатьяЗатрат
	|ИЗ
	|	РегистрРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Регистратор = &Регистратор
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	СтатьяЗатрат";
	Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Ссылка);
	
	ВыборкаПоСтатьеЗатрат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоСтатьеЗатрат.Следующий() Цикл
		
		ДвижениеУчетЗатрат = Движения.УчетЗатрат.Добавить();
		ДвижениеУчетЗатрат.Период = Дата;
		ЗаполнитьЗначенияСвойств(ДвижениеУчетЗатрат, ВыборкаПоСтатьеЗатрат);
		
		ВыборкаДетальные = ВыборкаПоСтатьеЗатрат.Выбрать();
		
		Пока ВыборкаДетальные.Следующий() Цикл
			
			ДвижениеХозрасчетный = Движения.Хозрасчетный.Добавить();
			ДвижениеХозрасчетный.СчетДт = ПланыСчетов.Хозрасчетный.РасходыНаПродажу;
			ДвижениеХозрасчетный.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
			ДвижениеХозрасчетный.Период = Дата;
			ДвижениеХозрасчетный.Сумма =  ВыборкаДетальные.Сумма;
			ДвижениеХозрасчетный.Содержание = "Отражено начисление премии сотрудникам";
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетДт, ДвижениеХозрасчетный.СубконтоДт, ВыборкаДетальные.СтатьяЗатрат);
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетКт, ДвижениеХозрасчетный.СубконтоКт, ВыборкаДетальные.Сотрудник);
			
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры
