
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	//{{__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТоваровИУслуг") Тогда
		// Заполнение шапки
		ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ВозвратОтПоставщика");
		Договор = ДанныеЗаполнения.Договор;
		Плательщик = ДанныеЗаполнения.Контрагент;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Реализация") Тогда
		// Заполнение шапки
		ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступленияДенег.ОплатаОтПокупателя");
		Плательщик = ДанныеЗаполнения.Контрагент;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
	КонецЕсли;
	//}}__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ДенежныеСредства.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
	Движение = Движения.ДенежныеСредства.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Касса = Касса;
	Движение.Сумма = СуммаДокумента;
	
	АналитикаПроводки = ПолучитьАналитикуПроводки();
	
	ДвижениеХозрасчетный = Движения.Хозрасчетный.Добавить();
	ДвижениеХозрасчетный.СчетДт = АналитикаПроводки.СчетДт;
	ДвижениеХозрасчетный.СчетКт = АналитикаПроводки.СчетКт;
	ДвижениеХозрасчетный.Период = Дата;
	ДвижениеХозрасчетный.Сумма = СуммаДокумента;
	ДвижениеХозрасчетный.Содержание = АналитикаПроводки.СодержаниеОперации;
	БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетДт, ДвижениеХозрасчетный.СубконтоДт, АналитикаПроводки.СубконтоДт);
	БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетКт, ДвижениеХозрасчетный.СубконтоКт, АналитикаПроводки.СубконтоКт);

КонецПроцедуры

Функция ПолучитьАналитикуПроводки()

	ОплатаОтПокупателя = Перечисления.ВидыОперацийПоступленияДенег.ОплатаОтПокупателя;
	ВозвратОтПоставщика = Перечисления.ВидыОперацийПоступленияДенег.ВозвратОтПоставщика;
	ВозвратОтПодотчетника = Перечисления.ВидыОперацийПоступленияДенег.ВозвратОтПодотчетника;
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("СчетДт", ПланыСчетов.Хозрасчетный.Касса);
	СтруктураОтбора.Вставить("СубконтоДт", Касса);
	
	Если ВидОперации = ОплатаОтПокупателя Тогда
		
		СтруктураОтбора.Вставить("СчетКт", ПланыСчетов.Хозрасчетный.РасчетыСПокупателями);
		СтруктураОтбора.Вставить("СубконтоКт", Плательщик);
		СтруктураОтбора.Вставить("СодержаниеОперации", "Оплата от покупателя наличными в кассу");
		
	ИначеЕсли ВидОперации = ВозвратОтПоставщика Тогда
		
		СтруктураОтбора.Вставить("СчетКт", ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками);
		СтруктураОтбора.Вставить("СубконтоКт", Плательщик);
		СтруктураОтбора.Вставить("СодержаниеОперации", "Возврат от поставщика наличными в кассу");
		
	ИначеЕсли ВидОперации = ВозвратОтПодотчетника Тогда
		
		СтруктураОтбора.Вставить("СчетКт", ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами);
		СтруктураОтбора.Вставить("СубконтоКт", Плательщик);
		СтруктураОтбора.Вставить("СодержаниеОперации", "Возврат подотчетных средств");
		
	КонецЕсли;
	
	Возврат СтруктураОтбора;

КонецФункции

