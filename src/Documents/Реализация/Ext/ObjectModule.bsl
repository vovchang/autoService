
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказНаряд") Тогда
		
		Автомобиль = ДанныеЗаполнения.Автомобиль;
		Контрагент = ДанныеЗаполнения.Клиент;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		Комментарий = СтрШаблон("Введен на основании: Заказ-наряд № %1 от %2", ДанныеЗаполнения.Номер, Формат(ДанныеЗаполнения.Дата, "ДЛФ=Д"));
		Склад = ДанныеЗаполнения.Склад;
		Сотрудник = ДанныеЗаполнения.Сотрудник;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
		
		Для Каждого ТекСтрокаМатериалыЗаказчика Из ДанныеЗаполнения.МатериалыЗаказчика Цикл
			НоваяСтрока = МатериалыЗаказчика.Добавить();
			
			НоваяСтрока.Количество = ТекСтрокаМатериалыЗаказчика.Количество;
			НоваяСтрока.Номенклатура = ТекСтрокаМатериалыЗаказчика.Номенклатура;
			НоваяСтрока.СостояниеМатериала = ТекСтрокаМатериалыЗаказчика.СостояниеМатериала;
			
		КонецЦикла;  
		
		Для Каждого ТекСтрокаУслуги Из ДанныеЗаполнения.Услуги Цикл
			
			НоваяСтрока = Услуги.Добавить();
			
			НоваяСтрока.Количество = ТекСтрокаУслуги.Количество;
			НоваяСтрока.Сумма = ТекСтрокаУслуги.Сумма;
			НоваяСтрока.Услуга = ТекСтрокаУслуги.Услуга;
			НоваяСтрока.Цена = ТекСтрокаУслуги.Цена; 
			
		КонецЦикла;  
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.ЗаказыКлиентов.Записывать = Истина;
	Движения.Продажи.Записывать = Истина; 
	Движения.УчетЗатрат.Записывать = Истина; 
	Движения.МатериалыЗаказчиков.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;

	
	Движения.ТоварыНаСкладах.Записать();
	Движения.ЗаказыКлиентов.Записать();  
	Движения.Продажи.Записать();
	Движения.УчетЗатрат.Записать();                  
	Движения.МатериалыЗаказчиков.Записать();
	Движения.Хозрасчетный.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыНаСкладах");
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реализация.Склад КАК Склад,
	|	РеализацияТовары.Номенклатура КАК Номенклатура,
	|	СУММА(РеализацияТовары.Количество) КАК Количество,
	|	СУММА(РеализацияТовары.Сумма) КАК Сумма
	|ПОМЕСТИТЬ вт_ТоварыКСписанию
	|ИЗ
	|	Документ.Реализация.Товары КАК РеализацияТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Реализация КАК Реализация
	|		ПО РеализацияТовары.Ссылка = Реализация.Ссылка
	|ГДЕ
	|	РеализацияТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Реализация.Склад,
	|	РеализацияТовары.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	РеализацияУслуги.Услуга,
	|	СУММА(РеализацияУслуги.Количество),
	|	СУММА(РеализацияУслуги.Сумма)
	|ИЗ
	|	Документ.Реализация.Услуги КАК РеализацияУслуги
	|ГДЕ
	|	РеализацияУслуги.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияУслуги.Услуга
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	РеализацияМатериалыЗаказчика.Номенклатура,
	|	СУММА(РеализацияМатериалыЗаказчика.Количество),
	|	СУММА(0)
	|ИЗ
	|	Документ.Реализация.МатериалыЗаказчика КАК РеализацияМатериалыЗаказчика
	|ГДЕ
	|	РеализацияМатериалыЗаказчика.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияМатериалыЗаказчика.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ТоварыКСписанию.Номенклатура.СтатьяЗатрат КАК СтатьяЗатрат,
	|	вт_ТоварыКСписанию.Номенклатура КАК Номенклатура,
	|	вт_ТоварыКСписанию.Номенклатура.Представление КАК НоменклатураПредставление,
	|	вт_ТоварыКСписанию.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТоварыНаСкладахОстатки.СрокГодности КАК СрокГодности,
	|	вт_ТоварыКСписанию.Количество КАК КоличествоКСписанию,
	|	ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ТоварыНаСкладахОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|	вт_ТоварыКСписанию.Сумма КАК СуммаДокумента,
	|	вт_ТоварыКСписанию.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	|ИЗ
	|	вт_ТоварыКСписанию КАК вт_ТоварыКСписанию
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				&МоментВремени,
	|				(Номенклатура, Склад) В
	|					(ВЫБРАТЬ
	|						вт_ТоварыКСписанию.Номенклатура,
	|						вт_ТоварыКСписанию.Склад
	|					ИЗ
	|						вт_ТоварыКСписанию КАК вт_ТоварыКСписанию)) КАК ТоварыНаСкладахОстатки
	|		ПО вт_ТоварыКСписанию.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура
	|ИТОГИ
	|	МАКСИМУМ(КоличествоКСписанию),
	|	СУММА(КоличествоОстаток),
	|	СУММА(СуммаОстаток),
	|	МАКСИМУМ(СуммаДокумента)
	|ПО
	|	СтатьяЗатрат,
	|	Номенклатура";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени()));
	
	ВыборкаСтатьяЗатрат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	СуммаВыручки = 0;
	
	Пока ВыборкаСтатьяЗатрат.Следующий() Цикл
		
		СуммаСебестоимости = 0; 
		
		ВыборкаНоменклатура = ВыборкаСтатьяЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаНоменклатура.Следующий() Цикл
			
			Если ВыборкаНоменклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар
				Или ВыборкаНоменклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Материал Тогда
				
				СуммаСебестоимостиПоНоменклатуре = 0;
				
				НехватаетКСписанию = ВыборкаНоменклатура.КоличествоКСписанию - ВыборкаНоменклатура.КоличествоОстаток; 
				Если НехватаетКСписанию > 0 Тогда
					Отказ = Истина;
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = СтрШаблон("На складе не хватает номенклатуры %1 в количестве %2", ВыборкаНоменклатура.НоменклатураПредставление, НехватаетКСписанию);
					Сообщение.Сообщить();
				Иначе
					ОсталосьСписать = ВыборкаНоменклатура.КоличествоКСписанию;
					
					ВыборкаДетальные = ВыборкаНоменклатура.Выбрать();
					
					Пока ВыборкаДетальные.Следующий() Цикл
						
						КоличествоКСписанию = МИН(ВыборкаДетальные.КоличествоОстаток, ОсталосьСписать);
						
						Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
						ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальные);
						Движение.Склад = Склад;
						Движение.Количество = КоличествоКСписанию; 
						Движение.Период = Дата;
						
						Если КоличествоКСписанию = ВыборкаДетальные.КоличествоОстаток Тогда
							СуммаКСписанию = ВыборкаДетальные.СуммаОстаток;
						Иначе
							СуммаКСписанию = ВыборкаДетальные.СуммаОстаток / ВыборкаДетальные.КоличествоОстаток * КоличествоКСписанию;
						КонецЕсли;
						
						Движение.Сумма = СуммаКСписанию;
						
						ОсталосьСписать = ОсталосьСписать - КоличествоКСписанию;    
						
						СуммаСебестоимости = СуммаСебестоимости + СуммаКСписанию;
						СуммаСебестоимостиПоНоменклатуре = СуммаСебестоимостиПоНоменклатуре + СуммаКСписанию;
						
						Если ОсталосьСписать = 0 Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
				КонецЕсли;
				
			ДвижениеХозрасчетный = Движения.Хозрасчетный.Добавить();	
			ДвижениеХозрасчетный.СчетДт = ПланыСчетов.Хозрасчетный.Продажи;
			ДвижениеХозрасчетный.СчетКт = ВыборкаНоменклатура.СчетБухгалтерскогоУчета;
			ДвижениеХозрасчетный.Период = Дата;
			ДвижениеХозрасчетный.Сумма = СуммаСебестоимостиПоНоменклатуре;
			ДвижениеХозрасчетный.Содержание = "Списана себестоимость товарно-материальных ценностей";
			ДвижениеХозрасчетный.СубконтоКт[ПланыВидовХарактеристик.ВидыCубконтоXозрасчетные.Номенклатура] = ВыборкаНоменклатура.Номенклатура;

			ИначеЕсли ВыборкаНоменклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.МатериалКлиента Тогда				
				Движение = Движения.МатериалыЗаказчиков.ДобавитьРасход();
				ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура);
				Движение.Период = Дата;
				Движение.Склад = Склад;
				Движение.Заказчик = Контрагент;
				Движение.Количество = ВыборкаНоменклатура.КоличествоКСписанию;				
			КонецЕсли;
			
			Если ВыборкаНоменклатура.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.МатериалКлиента Тогда				
				Движение = Движения.Продажи.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура);
				Движение.Количество = ВыборкаНоменклатура.КоличествоКСписанию;
				Движение.Сотрудник = Сотрудник;
				Движение.Контрагент = Контрагент;
				Движение.Сумма = ВыборкаНоменклатура.СуммаДокумента;
				Движение.Период = Дата;  
				
				СуммаВыручки = СуммаВыручки + ВыборкаНоменклатура.СуммаДокумента; 
			КонецЕсли;
						
		КонецЦикла;
		
		Если СуммаСебестоимости <> 0 Тогда			
			Движение = Движения.УчетЗатрат.Добавить();
			Движение.Период = Дата;
			Движение.СтатьяЗатрат = ВыборкаСтатьяЗатрат.СтатьяЗатрат;
			Движение.Сумма = СуммаСебестоимости;			
		КонецЕсли;
				
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		Движение = Движения.ЗаказыКлиентов.ДобавитьРасход();
		Движение.Клиент = Контрагент;
		Движение.Период = Дата;
		Движение.ЗаказКлиента = ДокументОснование;
		Движение.Сумма = Услуги.Итог("Сумма");
	КонецЕсли; 
	
	Если СуммаВыручки <> 0 Тогда 
		ДвижениеХозрасчетный = Движения.Хозрасчетный.Добавить();
		ДвижениеХозрасчетный.СчетДт = ПланыСчетов.Хозрасчетный.РасчетыСПокупателями;
		ДвижениеХозрасчетный.СчетКт = ПланыСчетов.Хозрасчетный.Продажи;
		ДвижениеХозрасчетный.Период = Дата;
		ДвижениеХозрасчетный.Сумма = СуммаВыручки;
		ДвижениеХозрасчетный.Содержание = "Отражена выручка от реализации товаров и услуг";
		БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетДт, ДвижениеХозрасчетный.СубконтоДт, Контрагент);			
	КонецЕсли;
	
КонецПроцедуры



