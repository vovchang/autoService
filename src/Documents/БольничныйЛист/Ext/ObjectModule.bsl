
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.Начисления.Записывать = Истина;
	Движения.УчетЗатрат.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КадроваяИсторияСотрудниковСрезПоследних.Оклад КАК Оклад
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(&Период, Сотрудник = &Сотрудник) КАК КадроваяИсторияСотрудниковСрезПоследних";
	Запрос.УстановитьПараметр("Период", ЭтотОбъект.ДатаОкончания);
	Запрос.УстановитьПараметр("Сотрудник", ЭтотОбъект.Сотрудник);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ПараметрРасчета = 0;
	Иначе
		ПараметрРасчета = РезультатЗапроса.Выгрузить()[0].Оклад;
	КонецЕсли;
	
	ТекущийМесяц = НачалоМесяца(ЭтотОбъект.ДатаНачала);
	ПоследнийМесяц = КонецМесяца(ЭтотОбъект.ДатаОкончания);
	
	Пока ТекущийМесяц <= ПоследнийМесяц Цикл
		
		НачалоПериода = Макс(ЭтотОбъект.ДатаНачала, ТекущийМесяц);
		КонецПериода  = Мин(ЭтотОбъект.ДатаОкончания, КонецМесяца(ТекущийМесяц));
		
		Движение = Движения.Начисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.Начисления.Больничный;
		Движение.ПериодДействияНачало = НачалоПериода;
		Движение.ПериодДействияКонец = КонецПериода;
		Движение.ПериодРегистрации = ПериодНачисления;
		Движение.БазовыйПериодНачало = НачалоПериода;
		Движение.БазовыйПериодКонец = КонецПериода;
		Движение.Сотрудник = Сотрудник;
		Движение.ПоказательРасчета = ПараметрРасчета; 
		
		ТекущийМесяц = ДобавитьМесяц(ТекущийМесяц, 1);
		
	КонецЦикла;
	
	Движения.Начисления.Записать();
	Движения.Начисления.РассчитатьСуммуНачисления();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ВидРасчета.СтатьяЗатрат КАК СтатьяЗатрат,
	|	СУММА(Начисления.Сумма) КАК Сумма
	|ИЗ
	|	РегистрРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления.Сотрудник,
	|	Начисления.ВидРасчета.СтатьяЗатрат";
	Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ДвижениеУчетЗатрат = Движения.УчетЗатрат.Добавить();
	ДвижениеУчетЗатрат.Период = Дата;
	ДвижениеУчетЗатрат.СтатьяЗатрат = Выборка.СтатьяЗатрат;
	ДвижениеУчетЗатрат.Сумма = Выборка.Сумма; 
	
	ДвижениеХозрасчетный = Движения.Хозрасчетный.Добавить();
	ДвижениеХозрасчетный.СчетДт = ПланыСчетов.Хозрасчетный.РасходыНаПродажу;
	ДвижениеХозрасчетный.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
	ДвижениеХозрасчетный.Период = Дата;
	ДвижениеХозрасчетный.Сумма =  Выборка.Сумма;
	ДвижениеХозрасчетный.Содержание = "Отражено начисление по больничному листу за счет работодателя";
	БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетДт, ДвижениеХозрасчетный.СубконтоДт, Выборка.СтатьяЗатрат);
	БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетКт, ДвижениеХозрасчетный.СубконтоКт, Выборка.Сотрудник);
	
КонецПроцедуры
