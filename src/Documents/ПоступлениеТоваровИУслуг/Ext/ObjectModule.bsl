
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.УчетЗатрат.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
	Движения.ТоварыНаСкладах.Записать();    
	Движения.УчетЗатрат.Записать();           
	Движения.Хозрасчетный.Записать();
		
	СтруктураСпособУчетаЗапасов = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	
	Если ЗначениеЗаполнено(СтруктураСпособУчетаЗапасов.СпособУчетаЗапасов) Тогда
		СпособУчетаЗапасов = СтруктураСпособУчетаЗапасов.СпособУчетаЗапасов;
	Иначе
		Отказ = Истина; 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнена учетная политика. В проведении документа отказано";
		Сообщение.Сообщить();
	КонецЕсли;
	
	Если Не Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПоступлениеТоваровИУслугТовары.Номенклатура КАК Номенклатура,
		|	ПоступлениеТоваровИУслугТовары.СрокГодности КАК СрокГодности,
		|	СУММА(ПоступлениеТоваровИУслугТовары.Количество) КАК Количество,
		|	СУММА(ПоступлениеТоваровИУслугТовары.Сумма) КАК Сумма,
		|	ПоступлениеТоваровИУслугТовары.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
		|ИЗ
		|	Документ.ПоступлениеТоваровИУслуг.Товары КАК ПоступлениеТоваровИУслугТовары
		|ГДЕ
		|	ПоступлениеТоваровИУслугТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровИУслугТовары.СрокГодности,
		|	ПоступлениеТоваровИУслугТовары.Номенклатура,
		|	ПоступлениеТоваровИУслугТовары.Номенклатура.СчетБухгалтерскогоУчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеТоваровИУслугУслуги.СтатьяЗатрат КАК СтатьяЗатрат,
		|	СУММА(ПоступлениеТоваровИУслугУслуги.Сумма) КАК Сумма,
		|	ПоступлениеТоваровИУслугУслуги.Услуга.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
		|ИЗ
		|	Документ.ПоступлениеТоваровИУслуг.Услуги КАК ПоступлениеТоваровИУслугУслуги
		|ГДЕ
		|	ПоступлениеТоваровИУслугУслуги.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровИУслугУслуги.СтатьяЗатрат,
		|	ПоступлениеТоваровИУслугУслуги.Услуга.СчетБухгалтерскогоУчета
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	СтатьяЗатрат";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);    
		
		ПакетЗапросов = Запрос.ВыполнитьПакет();
		
		ВыборкаПоНоменклатуре = ПакетЗапросов[0].Выбрать();
		
		Пока ВыборкаПоНоменклатуре.Следующий() Цикл
			
			Движение = Движения.ТоварыНаСкладах.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Склад = Склад;
			ЗаполнитьЗначенияСвойств(Движение, ВыборкаПоНоменклатуре);
						
			ДвижениеХозрасчетный = Движения.Хозрасчетный.Добавить();
			ДвижениеХозрасчетный.СчетДт = ВыборкаПоНоменклатуре.СчетБухгалтерскогоУчета;
			ДвижениеХозрасчетный.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
			ДвижениеХозрасчетный.Период = Дата;
			ДвижениеХозрасчетный.Сумма = ВыборкаПоНоменклатуре.Сумма;
			ДвижениеХозрасчетный.Содержание = "Отражено поступление товарно-материальных ценностей от поставщика";
			ДвижениеХозрасчетный.СубконтоДт[ПланыВидовХарактеристик.ВидыCубконтоXозрасчетные.Номенклатура] = ВыборкаПоНоменклатуре.Номенклатура;
			ДвижениеХозрасчетный.СубконтоКт[ПланыВидовХарактеристик.ВидыCубконтоXозрасчетные.Контрагенты] = Контрагент;
			
		КонецЦикла;
		
		
		ВыборкаПоСтатьеЗатрат = ПакетЗапросов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
		Пока ВыборкаПоСтатьеЗатрат.Следующий() Цикл
			
			СуммаЗатратПоСтатье = 0;      
			
			ВыборкаПоСчету = ВыборкаПоСтатьеЗатрат.Выбрать();
			
			Пока ВыборкаПоСчету.Следующий() Цикл
								
				ДвижениеХозрасчетный = Движения.Хозрасчетный.Добавить();
				ДвижениеХозрасчетный.СчетДт = ВыборкаПоСчету.СчетБухгалтерскогоУчета;
				ДвижениеХозрасчетный.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
				ДвижениеХозрасчетный.Период = Дата;
				ДвижениеХозрасчетный.Сумма = ВыборкаПоСчету.Сумма;
				ДвижениеХозрасчетный.Содержание = "Отражено поступление услуг от поставщика";
				БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетДт, ДвижениеХозрасчетный.СубконтоДт, ВыборкаПоСчету.СтатьяЗатрат);
				БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетКт, ДвижениеХозрасчетный.СубконтоКт, Контрагент);
				
				СуммаЗатратПоСтатье  = СуммаЗатратПоСтатье + ВыборкаПоСчету.Сумма; 
				
			КонецЦикла;
							
			Движение = Движения.УчетЗатрат.Добавить();
			Движение.Период = Дата;
			ЗаполнитьЗначенияСвойств(Движение, ВыборкаПоСтатьеЗатрат); 
			Движение.Сумма = СуммаЗатратПоСтатье;
						
		КонецЦикла;    
		
	КонецЕсли;
		
КонецПроцедуры

