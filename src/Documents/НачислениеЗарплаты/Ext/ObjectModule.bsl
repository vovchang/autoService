
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.Начисления.Записывать = Истина;
	движения.УчетЗатрат.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
	Для Каждого ТекСтрокаНачисления Из Начисления Цикл
		Движение = Движения.Начисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаНачисления.ВидРасчета;
		Движение.ПериодДействияНачало = ТекСтрокаНачисления.ДатаНачала;
		Движение.ПериодДействияКонец = ТекСтрокаНачисления.ДатаОкончания;
		Движение.ПериодРегистрации = ПериодНачисления;
		Движение.БазовыйПериодНачало = ТекСтрокаНачисления.ДатаНачала;
		Движение.БазовыйПериодКонец = ТекСтрокаНачисления.ДатаОкончания;
		Движение.Сотрудник = ТекСтрокаНачисления.Сотрудник;
		Движение.ПоказательРасчета = ТекСтрокаНачисления.ПоказательРасчета;
		Движение.График = ТекСтрокаНачисления.ГрафикРаботы;
	КонецЦикла;
	
	Движения.Начисления.Записать();
	Движения.Начисления.РассчитатьСуммуНачисления();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Сумма КАК Сумма,
	|	Начисления.ВидРасчета.СтатьяЗатрат КАК СтатьяЗатрат
	|ИЗ
	|	РегистрРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Регистратор = &Регистратор
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	СтатьяЗатрат";
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	ВыборкаПоСтатьеЗатрат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоСтатьеЗатрат.Следующий() Цикл
		
		ДвижениеУчетаЗатрат = Движения.УчетЗатрат.Добавить();
		ДвижениеУчетаЗатрат.Период = Дата;
		ЗаполнитьЗначенияСвойств(ДвижениеУчетаЗатрат, ВыборкаПоСтатьеЗатрат);
		
		ВыборкаДетальные = ВыборкаПоСтатьеЗатрат.Выбрать();
		
		Пока ВыборкаДетальные.Следующий() Цикл 
			
			ДвижениеХозрасчетный = Движения.Хозрасчетный.Добавить();
			ДвижениеХозрасчетный.СчетДт = ПланыСчетов.Хозрасчетный.РасходыНаПродажу;
			ДвижениеХозрасчетный.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда;
			ДвижениеХозрасчетный.Период = Дата;
			ДвижениеХозрасчетный.Сумма =  ВыборкаДетальные.Сумма;
			ДвижениеХозрасчетный.Содержание = "Отражено начисление заработной платы сотрудникам";
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетДт, ДвижениеХозрасчетный.СубконтоДт, ВыборкаДетальные.СтатьяЗатрат);
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетКт, ДвижениеХозрасчетный.СубконтоКт, ВыборкаДетальные.Сотрудник);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры
