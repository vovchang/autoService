
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	//{{__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТоваровИУслуг") Тогда
		// Заполнение шапки
		Договор = ДанныеЗаполнения.Договор;
		Получатель = ДанныеЗаполнения.Контрагент;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
		ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходаДенег.ОплатаПоставщику");
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Реализация") Тогда
		// Заполнение шапки
		ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходаДенег.ВозвратПокупателю");
		Получатель = ДанныеЗаполнения.Контрагент;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
	КонецЕсли;
	//}}__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ДенежныеСредства.Записывать = Истина;
	Движения.Хозрасчетный.Записывать = Истина;
	
	Движение = Движения.ДенежныеСредства.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.Касса = Касса;
	Движение.Сумма = СуммаДокумента;
	
	АналитикаПроводки = ПолучитьАналитикуПроводки();
	
	ДвижениеХозрасчетный = Движения.Хозрасчетный.Добавить();
	ДвижениеХозрасчетный.СчетДт = АналитикаПроводки.СчетДт;
	ДвижениеХозрасчетный.СчетКт = АналитикаПроводки.СчетКт;
	ДвижениеХозрасчетный.Период = Дата;
	ДвижениеХозрасчетный.Сумма = СуммаДокумента;
	ДвижениеХозрасчетный.Содержание = АналитикаПроводки.СодержаниеОперации;
	БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетДт, ДвижениеХозрасчетный.СубконтоДт, АналитикаПроводки.СубконтоДт);
	БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(ДвижениеХозрасчетный.СчетКт, ДвижениеХозрасчетный.СубконтоКт, АналитикаПроводки.СубконтоКт);
	
КонецПроцедуры 

Функция ПолучитьАналитикуПроводки()

	ЭтоОплатаПоставщику = Перечисления.ВидыОперацийРасходаДенег.ОплатаПоставщику;
	ЭтоВозвратДенегПокупателю = Перечисления.ВидыОперацийРасходаДенег.ВозвратПокупателю;
	ЭтоВыплатаЗаработнойПлаты = Перечисления.ВидыОперацийРасходаДенег.ВыдачаЗаработнойПлаты;
	ЭтоВыдачаДенегПодотчетнику = Перечисления.ВидыОперацийРасходаДенег.ВыдачаПодотчетнику;
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("СчетКт", ПланыСчетов.Хозрасчетный.Касса);
	СтруктураОтбора.Вставить("СубконтоКт", Касса);
	
	Если ВидОперации = ЭтоОплатаПоставщику Тогда
		
		СтруктураОтбора.Вставить("СчетДт", ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками); 
		СтруктураОтбора.Вставить("СубконтоДт", Получатель);
		СтруктураОтбора.Вставить("СодержаниеОперации", "Оплата поставщику наличными из кассы");
		
	ИначеЕсли ВидОперации = ЭтоВозвратДенегПокупателю Тогда
		
		СтруктураОтбора.Вставить("СчетДт", ПланыСчетов.Хозрасчетный.РасчетыСПокупателями); 
		СтруктураОтбора.Вставить("СубконтоДт", Получатель);
		СтруктураОтбора.Вставить("СодержаниеОперации", "Возврат покупателю наличными из кассы");
		
	ИначеЕсли ВидОперации = ЭтоВыплатаЗаработнойПлаты Тогда
		
		СтруктураОтбора.Вставить("СчетДт", ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда); 
		СтруктураОтбора.Вставить("СубконтоДт", Получатель);
		СтруктураОтбора.Вставить("СодержаниеОперации", "Выплата заработной платы наличными из кассы");
		
	ИначеЕсли ВидОперации = ЭтоВыдачаДенегПодотчетнику Тогда
		
		СтруктураОтбора.Вставить("СчетДт", ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами); 
		СтруктураОтбора.Вставить("СубконтоДт", Получатель);
		СтруктураОтбора.Вставить("СодержаниеОперации", "Выдача денежных средств подотчётному лицу");
		
	КонецЕсли;
	
	Возврат СтруктураОтбора;
	
КонецФункции

