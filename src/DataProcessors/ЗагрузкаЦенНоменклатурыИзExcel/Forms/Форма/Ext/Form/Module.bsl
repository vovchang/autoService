
&НаКлиенте
Процедура ЗагрузитьПрайсЛист(Команда)
	СкопироватьФайлНаСервер("ПостроительЗапроса");
КонецПроцедуры

&НаКлиенте 
Процедура СкопироватьФайлНаСервер(СпособЗагрузки)

	ОповещениеОЗавершении = Новый ОписаниеОповещения("СкопироватьФайлНаСерверЗавершение", ЭтотОбъект, СпособЗагрузки);
	НачатьПомещениеФайлаНаСервер(ОповещениеОЗавершении,,,,,УникальныйИдентификатор);

 КонецПроцедуры                                                                        
 
 &НаКлиенте 
Процедура СкопироватьФайлНаСерверЗавершение(ОписаниеФайла, ДополнительныеПараметры) Экспорт
	 
	 Если ОписаниеФайла <> Неопределено Тогда
		 
		 АдресФайлаВХранилище = ОписаниеФайла.Адрес;
		 
		 ЗагрузитьИзТабличногоДокументаНаСервере(АдресФайлаВХранилище, ДатаПрайсЛиста, ВидЦеныПродажи, ДополнительныеПараметры);
 	
	 КонецЕсли;
	 
КонецПроцедуры
 
&НаСервереБезКонтекста
Процедура ЗагрузитьИзТабличногоДокументаНаСервере(АдресФайлаВХранилище, ДатаПрайсЛиста, ВидЦеныПродажи, ДополнительныеПараметры)
	
	ТабДок = ПрочитатьФайл(АдресФайлаВХранилище);
	ТаблицаПрайсЛиста = ЗаполнитьТаблицуЗначенийЧерезПостроительЗапроса(ТабДок);
	
	Если ТаблицаПрайсЛиста.Количество() Тогда
		ЗагрузитьПрайсЛистПоставщика(ТаблицаПрайсЛиста, ДатаПрайсЛиста, ВидЦеныПродажи);
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Выбранный файл не содержит строк с ценами!";
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста                            
Функция ПрочитатьФайл(АдресФайлаВХранилище)
	
	ТабДок = Новый ТабличныйДокумент;
	ИмяВременогоФайла = ПолучитьИмяВременногоФайла(".xlsx");
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайлаВХранилище);
	ДвоичныеДанные.Записать(ИмяВременогоФайла);
	
	Попытка
		ТабДок.Прочитать(ИмяВременогоФайла);
	Исключение  
		вызватьИсключение "Не удалось прочитать файл EXCEL в табличный документ!";
	КонецПопытки; 
	
	возврат ТабДок;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗагрузитьПрайсЛистПоставщика(ТаблицаПрайсЛиста, ДатаПрайсЛиста, ВидЦеныПродажи)
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрайсЛист.N КАК N,
	|	ПрайсЛист.Номенклатура КАК Номенклатура,
	|	ПрайсЛист.Код КАК Код,
	|	ПрайсЛист.Цена КАК Цена
	|ПОМЕСТИТЬ вт_ТаблицаПрайсЛиста
	|ИЗ
	|	&ПрайсЛист КАК ПрайсЛист
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Ном.Ссылка КАК Номенклатура,
	|	вт_ТаблицаПрайсЛиста.N КАК НомерСтроки,
	|	вт_ТаблицаПрайсЛиста.Цена КАК Цена
	|ИЗ
	|	вт_ТаблицаПрайсЛиста КАК вт_ТаблицаПрайсЛиста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Ном
	|		ПО (вт_ТаблицаПрайсЛиста.Код = Ном.Код
	|				ИЛИ вт_ТаблицаПрайсЛиста.Номенклатура = Ном.Наименование)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	Запрос.УстановитьПараметр("ПрайсЛист", ТаблицаПрайсЛиста);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
    КоличествоЗагруженныхСтрок = 0;
	ЕстьОшибкиПриЗагрузке = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		Запись = РегистрыСведений.ЦеныНоменклатуры.СоздатьНаборЗаписей();
		
		Запись.Отбор.Номенклатура.Значение = Выборка.Номенклатура;
		Запись.Отбор.Номенклатура.Использование = Истина;
		
		Запись.Отбор.Период.Значение = ДатаПрайсЛиста;
		Запись.Отбор.Период.Использование = Истина;
		
		Запись.Отбор.ВидЦены.Значение = ВидЦеныПродажи;  
		Запись.Отбор.ВидЦены.Использование = Истина;
		
		
		НоваяСтрока = Запись.Добавить();
		НоваяСтрока.Номенклатура = Выборка.Номенклатура;
		НоваяСтрока.ВидЦены = ВидЦеныПродажи;
		НоваяСтрока.Цена = Выборка.Цена;
		НоваяСтрока.Период = ДатаПрайсЛиста;
		
		Попытка
			
			Запись.Записать();
			КоличествоЗагруженныхСтрок = КоличествоЗагруженныхСтрок + 1;
			
		Исключение
			
			ЕстьОшибкиПриЗагрузке = Истина;
			
		КонецПопытки;
		
	КонецЦикла;
		
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("Данные загруженны. Количество успешно загруженных строк %1", КоличествоЗагруженныхСтрок);
	Сообщение.Сообщить();
	
	Если ЕстьОшибкиПриЗагрузке Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Произошла ошибка при записи в регистр сведений цены номенклатуры!";
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполнитьТаблицуЗначенийЧерезПостроительЗапроса(ТабДок)

	ОбластиТабличногоДокумента = ТабДок.Область(1, 1, ТабДок.ВысотаТаблицы, ТабДок.ШиринаТаблицы); 

	Построитель = Новый ПостроительЗапроса; 
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ОбластиТабличногоДокумента); 
	Построитель.Выполнить();

	ТаблицаПрайсЛиста = Построитель.Результат.Выгрузить();

	возврат ТаблицаПрайсЛиста;
	
КонецФункции

