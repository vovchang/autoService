&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗаполнитьТипНоменклатурыПоРодителю();
	КонецЕсли;
	 
	 УстановитьСписокВыбораСчетаБухгалтерскогоУчета(); 
	
КонецПроцедуры

 &НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьИДоступностьЭлементов();
		
КонецПроцедуры

&НаКлиенте
Процедура ТипНоменклатурыПриИзменении(Элемент)
	
	УстановитьВидимостьИДоступностьЭлементов();
	
	Объект.СчетБухгалтерскогоУчета = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПустаяСсылка");
	
	УстановитьСписокВыбораСчетаБухгалтерскогоУчета();
	
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	ЗаполнитьТипНоменклатурыПоРодителю();
	
	Объект.СчетБухгалтерскогоУчета = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПустаяСсылка");
	
	УстановитьСписокВыбораСчетаБухгалтерскогоУчета();
	
КонецПроцедуры

&НаСервере 
Процедура ЗаполнитьТипНоменклатурыПоРодителю()
	
	Если ЗначениеЗаполнено(Объект.Родитель.ТипНоменклатуры) Тогда
		Объект.ТипНоменклатуры = Объект.Родитель.ТипНоменклатуры;
	Иначе
		Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Процедура УстановитьВидимостьИДоступностьЭлементов()

	Элементы.ДлительностьУслуги.Видимость = (Объект.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга"));
	Элементы.СчетБухгалтерскогоУчета.Доступность = Не (Объект.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МатериалКлиента"));

КонецПроцедуры

&НаСервере 
Процедура УстановитьСписокВыбораСчетаБухгалтерскогоУчета()
	
	МассивЗначений = Новый Массив;
	
	Если Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар Тогда
		
		МассивЗначений.Добавить(ПланыСчетов.Хозрасчетный.Товары);
		
	ИначеЕсли Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Материал Тогда
		
		МассивЗначений.Добавить(ПланыСчетов.Хозрасчетный.Материалы);
		
	ИначеЕсли Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
		
		МассивЗначений.Добавить(ПланыСчетов.Хозрасчетный.РасходыНаПродажу);
		МассивЗначений.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы);
		
	КонецЕсли;
	
	Элементы.СчетБухгалтерскогоУчета.СписокВыбора.ЗагрузитьЗначения(МассивЗначений);

КонецПроцедуры









