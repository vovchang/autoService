&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.НачалоПериода = '00010101' И Объект.КонецПериода = '00010101' Тогда
		Объект.НачалоПериода = НачалоГода(ТекущаяДата());
		Объект.КонецПериода = КонецГода(ТекущаяДата());
	КонецЕсли;
	
	Если Объект.СпособЗаполнения = 0 Тогда
		Объект.СпособЗаполнения = 1;
	КонецЕсли;
	
	Если Объект.НачалоГрафика = 0 Тогда
		Объект.НачалоГрафика = 1;
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	ПроверкаПериода();

КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)

	ПроверкаПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаполненияПриИзменении(Элемент)

	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьГрафик(Команда)
	
	Объект.СписокДней.Очистить();
	
	Если Объект.СпособЗаполнения = 1 Тогда
		ЗаполнитьГрафикПятидневка();
	ИначеЕсли Объект.СпособЗаполнения = 2 Тогда
		ЗаполнитьЦикличныйГрафик();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьВГрафикиПредприятияНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГрафикиРаботыСотрудниковСписокДней.Ссылка КАК Ссылка,
	|	ГрафикиРаботыСотрудниковСписокДней.День КАК День,
	|	ГрафикиРаботыСотрудниковСписокДней.КоличествоЧасов КАК КоличествоЧасов
	|ИЗ
	|	Справочник.ГрафикиРаботыСотрудников.СписокДней КАК ГрафикиРаботыСотрудниковСписокДней
	|ГДЕ
	|	ГрафикиРаботыСотрудниковСписокДней.Ссылка = &Ссылка
	|	И ГрафикиРаботыСотрудниковСписокДней.КоличествоЧасов > 0";
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Результат = Запрос.Выполнить().Выгрузить();
	РегистрыСведений.ГрафикиРаботыПредприятия.ЗагрузкаГрафикаВРС(Результат);
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("График работы %1 успешно загружен.", Объект.Наименование);
	Сообщение.Сообщить();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВГрафикиПредприятия(Команда)
	ЗагрузитьВГрафикиПредприятияНаСервере();
КонецПроцедуры

&НаКлиенте 
Процедура УстановитьВидимость()

	ЭтоЦикличныйГрафик = (Объект.СпособЗаполнения = 2);
	
	Элементы.ШагДней.Видимость 			= ЭтоЦикличныйГрафик;
	Элементы.НачалоГрафика.Видимость 	= ЭтоЦикличныйГрафик;

КонецПроцедуры

&НаКлиенте 
Процедура ПроверкаПериода()
	
	Если ЗначениеЗаполнено(Объект.КонецПериода) 
		И Объект.НачалоПериода > Объект.КонецПериода Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Начало периода не может быть больше окончания периода.";
		Сообщение.Сообщить();
		
		Объект.КонецПериода = '00010101';
		
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте 
Процедура ЗаполнитьГрафикПятидневка()

	СуткиВСекундах = 86400;
	ТекущийДень = Объект.НачалоПериода;
	
	Пока ТекущийДень <= Объект.КонецПериода Цикл
		
		НоваяСтрока = Объект.СписокДней.Добавить();
		НоваяСтрока.День = ТекущийДень;
		
		ЭтоРабочийДень = (ДеньНедели(ТекущийДень) < 6);
		
		Если ЭтоРабочийДень Тогда
			НоваяСтрока.КоличествоЧасов = Объект.КоличествоЧасов;
		КонецЕсли;
		
		ТекущийДень = ТекущийДень + СуткиВСекундах;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЦикличныйГрафик()

	СуткиВСекундах = 86400;
	ТекущийДень = Объект.НачалоПериода;
	
	Если Объект.НачалоГрафика = 1 Тогда
		ЭтоРабочийДень = Истина;
	Иначе
		ЭтоРабочийДень = Ложь;
	КонецЕсли;
	
	Сч = 0; 
	
	Пока ТекущийДень <= Объект.КонецПериода Цикл
		
		НоваяСтрока = Объект.СписокДней.Добавить();
		НоваяСтрока.День = ТекущийДень;
		
		Если ЭтоРабочийДень Тогда
			НоваяСтрока.КоличествоЧасов = Объект.КоличествоЧасов;
		КонецЕсли;
		
		Сч = Сч + 1;
		ТекущийДень = ТекущийДень + СуткиВСекундах;
		
		Если Сч % Объект.ШагДней = 0 Тогда
			ЭтоРабочийДень = Не ЭтоРабочийДень;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры






 




